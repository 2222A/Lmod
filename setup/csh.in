#!/bin/csh
setenv LMOD_CMD "@PKG@/libexec/lmod"
setenv LMOD_DIR "@PKG@/libexec"

set prefix  = ""
set midfix  = ""
set postfix = ";"

set histchar = \!
if ( $?histchars) then
  set histchar   = `echo $histchars | cut -c1`
  if ( "$histchar" != '\!' ) then
    set _histchars = $histchars
    set prefix  = 'set _histchars = $histchars; unset histchars;'
    set midfix  = '--localvar histchars=$_histchars'
    set postfix = ';set histchars = $_histchars'
  endif
endif

alias module  $prefix'eval `$LMOD_CMD csh '$midfix $histchar'*` '$postfix' eval `$LMOD_SETTARG_CMD -s csh`'
alias clearMT 'eval `'$LMOD_DIR/clearMT_cmd csh'`'

alias ml      'eval `'$LMOD_DIR/ml_cmd' '$histchar'*`'

unset prefix
unset midfix
unset postfix
unset histchar

alias cwdcmd 'eval `$LMOD_SETTARG_CMD -s csh`'

if ( $?prompt ) then
   switch ($TERM)
       case "xterm*":
            alias precmd 'echo -n "\033]0;${TARG_TITLE_BAR_PAREN}${HOST}:$cwd\007"'
            breaksw
   endsw
endif
# Local Variables:
# mode: shell-script
# end:

